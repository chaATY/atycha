![](https://img.shields.io/badge/更新时间-2023.10.30-yellow.svg)
![](https://img.shields.io/badge/C-17-green.svg)

#

# 一、前言

GCC 支持在 C/C++代码中嵌入汇编代码，这些汇编代码被称作 GCC Inline ASM——GCC 内联汇编。这是一个非常有用的功能，有利于我们将一些 C/C++语法无法表达的指令直接潜入 C/C++代码中，另外也允许我们直接写 C/C++代码中使用汇编编写简洁高效的代码。

优点：效率高。

实现：使用 asm 关键字。

关键：之所以内联汇编如此有用，主要是因为它可以操作 C 语言变量，比如可以从 C 语言变量获取值，输出值到 C 语言变量。由于这个能力，asm 用作汇编指令和包含它的 C 程序之间的接口。

---

# 二、内联汇编实现加法函数

简单示例：

```c
#include <stdio.h>

int main()
{
	int a = 0, b = 0, c = 0;

	__asm
	{
		/*push eax s*/
		mov a, 1000
		mov b, 200
		mov c, 1
		/*mov eax, a
		add eax, b
		sub eax, c
		mov a, eax */
		/*pop eax*/
	}
	/*__asm mov a,1000*/

	printf("a=%d\nb=%d\nc=%d\n", a, b, c);

	return 0;
}
```

`__declspec(naked)` 是用来告诉编译器函数代码的汇编语言为自己的所写，不需要编译器添加任何汇编代码，可选。

注意，`__declspec(naked)`是编译器直接拿来用的汇编函数代码，所以一定要记得在开始的时候保存上下文标志位（压栈），在结束的时候要记得恢复上下文（出栈）。并且在结尾要加上 ret 命令。

自写加法函数

```c
#include "stdafx.h"

int __declspec (naked) Plus(int x,int y){
                __asm{
                //保留调用前堆栈
                push ebp
                //提升堆栈
                mov ebp,esp
                sub esp,0x40
                //保护现场
                push ebx
                push esi
                push edi
                //初始化提升的堆栈，填充缓冲区
                mov eax,0xCCCCCCCC
                mov ecx,0x10
                lea edi,dword ptr ds:[ebp-0x40]
                rep stosd
                //函数核心功能

                //取出参数
                mov eax,dword ptr ds:[ebp+8]
                //参数相加
                add eax,dword ptr ds:[ebp+0xC]

                //恢复现场
                pop edi
                pop esi
                pop ebx

                //降低堆栈
                mov esp,ebp
                pop ebp

                //返回
                ret
        }
}
int main(int argc, char* argv[])
{
        Plus(1,2);
        return 0;
}
```

不难发现，其实我们自己实现的加法函数就是模拟了编译器为我们做的事情，此时进到函数内部也会看到

**函数内部：**

执行的就是我们自己所写的代码，而非编译器所生成的，并且也能够实现加法函数的功能

```asm
6:    int __declspec (naked) Plus(int x,int y){
00401030   push        ebp
7:        __asm{
8:            //保留调用前堆栈
9:            push ebp
10:           //提升堆栈
11:           mov ebp,esp
00401031   mov         ebp,esp
12:           sub esp,0x40
00401033   sub         esp,40h
13:           //保护现场
14:           push ebx
00401036   push        ebx
15:           push esi
00401037   push        esi
16:           push edi
00401038   push        edi
17:           //初始化提升的堆栈，填充缓冲区
18:           mov eax,0xCCCCCCCC
00401039   mov         eax,0CCCCCCCCh
19:           mov ecx,0x10
0040103E   mov         ecx,10h
20:           lea edi,dword ptr ds:[ebp-0x40]
00401043   lea         edi,ds:[ebp-40h]
21:           rep stosd
00401047   rep stos    dword ptr [edi]
22:           //函数核心功能
23:
24:           //取出参数
25:           mov eax,dword ptr ds:[ebp+8]
00401049   mov         eax,dword ptr ds:[ebp+8]
26:           //参数相加
27:           add eax,dword ptr ds:[ebp+0xC]
0040104D   add         eax,dword ptr ds:[ebp+0Ch]
28:
29:
30:           //恢复现场
31:           pop edi
00401051   pop         edi
32:           pop esi
00401052   pop         esi
33:           pop esi
00401053   pop         esi
34:
35:           //降低堆栈
36:           mov esp,ebp
00401054   mov         esp,ebp
37:           pop ebp
00401056   pop         ebp
38:
39:           //返回
40:           ret
```

**函数返回后：**

```asm
45:           Plus(1,2);
00401078   push         2
0040107A   push         1
0040107C   call         @ILT+15(function) (00401014)
00401081   add          esp,8
46:           return 0;
00401084   xor          eax,eax
47:    }
00401086   pop          edi
00401087   pop          esi
00401088   pop          ebx
00401089   add          esp,40h
0040108C   cmp          ebp,esp
0040108E   call         __chkesp (004010e0)
00401093   move         esp,ebp
00401095   pop          ebp
00401096   ret
```

我们可以发现函数返回后和普通函数并无差异 `00401081   add         esp,8`。都有这一行平衡堆栈的语句，也就是堆栈外平衡。

但如果我们想要在函数内部就平衡堆栈，也就是实现堆栈内平衡，也就是希望函数返回后没有这个外部的堆栈平衡语句，让堆栈的平衡工作由我们自己来处理，该如何做到？

这里就要引入 C 语言的调用协定这个概念了

---

# 三、调用协定

常见的几种调用协定：**其中\_\_cdecl 为 C 语言默认调用协定**
|调用协定|参数压栈顺序|平衡堆栈|
|---|---|---|
|**cdecl|从右至左入栈|调用者清理栈|
|**stdcall|从右至左入栈|自身清理堆栈|
|\_\_fastcall|ECX/EDX 传送前两个 剩下:从右至左入栈|自身清理堆栈|

接下来我们来比较一下这三种调用协定

```c
int __cdecl Plus1(int x,int y){
        return x+y;
}
int __stdcall Plus2(int x,int y){
        return x+y;
}
int __fastcall Plus3(int x,int y){
        return x+y;
}
```

同样都是一个简单的加法函数，分别采用了三种不同的调用协定，我们来用汇编来一察他们的区别：
||**cdecl|**stdcall|\_\_fastcall|
|---|---|---|---|
|参数压栈|push 2 push 1|push 2 push 1|mov edx,2 mov ecx,1|
|函数返回值|ret|ret 8|ret|
|返回后执行语句|add esp,8|xor eax,eax|xor eax,eax|

我们可以得出结论：

- \_\_cdecl 是将参数压入栈中，然后在函数执行返回后再平衡堆栈，也就是堆栈外平衡
- \_\_stdcall 也是将参数压入栈中，但是是在函数内部通过 ret xxx 来平衡堆栈，也就是堆栈内平衡
- \_\_fastcall 则是在参数个数小于等于 2 时直接使用 edx 和 ecx 作为参数传递的载体，没用使用到堆栈，自然也就无须平衡堆栈，但是当参数个数大于 2 时，则多出来的那几个参数则按 stdcall 的方式来处理，也是采用堆栈内平衡

接下来再谈谈\_\_stdcall 中返回值的问题：

我们可以看到，我们在上面的加法函数中 push 了两个立即数 2 和 1，返回值是 8。这是不是意味着 ret xxxx 中 xxxx=参数个数\*4？并不是！！！这里 ret xxxx 里的 xxxx 和压入参数的数据宽度有关

我们这里压入的两个立即数的数据宽度都是 4 个字节=32bit，因此我们这里是 ret 4+4=8。如果改成 push ax，也就是压入 2 个字节=16bit 时则应该 ret 2。这里可以参考堆栈篇中堆栈相关汇编指令的 push 指令

了解了以上调用协定后，我们就可以修改之前的简单加法裸函数，将其改为堆栈内平衡

---

# 四、堆栈内平衡加法函数

与前面相比，修改 ret 为 ret 8，自己在函数内实现了堆栈内平衡

```c
__declspec (naked) __stdcall int  Plus(int x,int y){
                __asm{
                //保留调用前堆栈
                push ebp
                //提升堆栈
                mov ebp,esp
                sub esp,0x40
                //保护现场
                push ebx
                push esi
                push edi
                //初始化提升的堆栈，填充缓冲区
                mov eax,0xCCCCCCCC
                mov ecx,0x10
                lea edi,dword ptr ds:[ebp-0x40]
                rep stosd
                //函数核心功能

                //取出参数
                mov eax,dword ptr ds:[ebp+8]
                //参数相加
                add eax,dword ptr ds:[ebp+0xC]
                //恢复现场
                pop edi
                pop esi
                pop ebx
                //降低堆栈
                mov esp,ebp
                pop ebp

                //返回
                ret 8
        }
}
int main(int argc, char* argv[])
{
        Plus(1,2);
        return 0;
}
```

---

> 版权声明 ©：
>
> 本文为 CHA.ATY 的原创文章，遵循 [CC BY-NC-SA 4.0](https://creativecommons.org/licenses/by-sa/4.0/) 许可证进行授权，转载请附上原文出处链接及本声明。
>
> 作者：CHA.ATY
>
> 邮箱：2165150141@qq.com
